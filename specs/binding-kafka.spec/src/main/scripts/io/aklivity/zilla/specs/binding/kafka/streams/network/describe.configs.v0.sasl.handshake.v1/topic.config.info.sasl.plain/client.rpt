#
# Copyright 2021-2023 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property networkConnectWindow 8192

property newRequestId ${kafka:newRequestId()}
property fetchWaitMax 500
property fetchBytesMax 65535
property partitionBytesMax 8192

connect "zilla://streams/net0"
  option zilla:window ${networkConnectWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

connected

write 17                                # size
      17s                               # sasl.handshake
      1s                                # v1
      ${newRequestId}
      -1s                               # no client id
      5s "PLAIN"                        # mechanism

read 17                                 # size
     ${newRequestId}
     0s                                 # no error
     1                                  # mechanisms
       5s "PLAIN"                         # PLAIN

write 32                                # size
      36s                               # sasl.authenticate
      1s                                # v1
      ${newRequestId}
      -1s                               # no client id
      18
      [0x00] "username"                 # authentication bytes
      [0x00] "password"

read 20                                 # size
     ${newRequestId}
     0s                                 # no error
     -1
     -1s                                # authentication bytes
     0L                                 # session lifetime

write 41                                # size
      32s                               # describe configs
      0s                                # v0
      ${newRequestId}
      -1s                               # no client id
      1                                 # resources
        [0x02]                          # topic resource
        4s "test"                       # "test" topic
        1                               # configs
          14s "cleanup.policy"            # name

read 55                                 # size
     ${newRequestId}
     [0..4]
     1                                  # resources
       0s                                 # no error
       -1s                                # error message
       [0x02]                             # topic resource
       4s "test"                          # "test" topic
       1                                  # configs
         14s "cleanup.policy"               # name
         7s "compact"                       # value
        [0x00]                              # not read only
        [0x00]                              # not default
        [0x00]                              # not sensitive
