#
# Copyright 2021-2023 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property networkConnectWindow 8192

property newRequestId ${kafka:newRequestId()}
property fetchWaitMax 500
property fetchBytesMax 65535
property partitionBytesMax 8192

connect "zilla://streams/net0"
  option zilla:window ${networkConnectWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

connected

write  20                                # size
       10s                               # find coordinator
       1s                                # v1
       ${newRequestId}
       -1s                               # no client id
       7s "session"                      # "session" coordinator key
       [0x00]                            # coordinator group type

read  35                                 # size
      ${newRequestId}
      0                                  #throttle time
      0s                                 #no error
      4s "none"                          #error message none
      1                                  #coordinator node
         9s "localhost"                  #host
         9092                            #port

write close

read notify ROUTED_BROKER_SERVER

connect await ROUTED_BROKER_SERVER
        "zilla://streams/net0"
  option zilla:window ${networkConnectWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

connected

write 62                                # size
      11s                               # join group
      5s                                # v5
      ${newRequestId}
      -1s                               # no client id
      4s "test"                         # consumer group
      45000                             # session timeout
      3000                              # rebalance timeout
      0s                                # consumer group member
      7s "client1"                      # group instance id
      8s "consumer"                     # protocol type
      1                                 # group protocol
        7s "session"                    # protocol name
        0                               # metadata


read  34                                                # size
      ${newRequestId}
      0                                                 # throttle time
      79s                                               # member id required
      -1                                                # generated id
      0s                                                # protocol name
      0s                                                # leader id
      10s "consumer-1"                                  # consumer member group id
      0                                                 # members

write 72                                # size
      11s                               # join group
      5s                                # v5
      ${newRequestId}
      -1s                               # no client id
      4s "test"                         # consumer group
      45000                             # session timeout
      3000                              # rebalance timeout
      10s "consumer-1"                  # consumer group member
      7s "client1"                      # group instance id
      8s "consumer"                     # protocol type
      1                                 # group protocol
        7s "session"                    # protocol name
        0                               # metadata


read  67                                                # size
      ${newRequestId}
      0                                                 # throttle time
      0s                                                # no error
      3                                                 # generated id
      7s "session"                                      # protocol name
      10s "consumer-1"                                  # leader id
      10s "consumer-1"                                  # consumer member group id
      1                                                 # members
         10s "consumer-1"                                  # consumer member group id
         0                                                 # metadata


write 61                                            # size
      14s                                           # sync group
      3s                                            # v3
      ${newRequestId}
      -1s                                           # no client id
      4s "test"                                     # consumer group
      2                                             # generation id
      10s "consumer-1"                              # consumer member group id
      7s "client1"                                  # group instance id
      1                                             # assignments
        10s "consumer-1"                             # consumer member group id
        0                                            # metadata

read  14                                                # size
      ${newRequestId}
      0                                                 # throttle time
      0s                                                # no error
      0                                                 # assignment

write 41                                            # size
      12s                                           # heartbeat
      3s                                            # 3v
      ${newRequestId}
      -1s                                           # no client id
      4s "test"                                     # consumer group
      3                                             # generation id
      10s "consumer-1"                              # consumer member group id
      7s "client1"                                  # group instance id

read 10                                                # size
      ${newRequestId}
      0                                                # throttle time
      0s                                               # no error

write abort
read aborted
