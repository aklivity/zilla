#
# Copyright 2021-2023 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property networkConnectWindow 8192
property instanceId ${kafka:randomBytes(42)}

property newRequestId ${kafka:newRequestId()}
property fetchWaitMax 500
property fetchBytesMax 65535
property partitionBytesMax 8192

connect "zilla://streams/net0"
  option zilla:window ${networkConnectWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

connected

write  22                                # size
       10s                               # find coordinator
       1s                                # v1
       ${newRequestId}
       5s "zilla"                        # client id
       4s "test"                         # "session" coordinator key
       [0x00]                            # coordinator group type

read  35                                 # size
      (int:newRequestId)
      0                                  # throttle time
      0s                                 # no error
      4s "none"                          # error message none
      0                                  # coordinator node
      9s "localhost"                     # host
      9092                               # port

write 82                                                 # size
      32s                                                # describe configs
      0s                                                 # v0
      ${newRequestId}
      -1s                                                # no client id
      1                                                  # resources
        [0x04]                                           # broker resource
        1s "0"                                           # "node" topic
        2                                                # configs
          28s "group.min.session.timeout.ms"             # name
          28s "group.max.session.timeout.ms"             # name

read 103                                                # size
      (int:newRequestId)
      0
      1                                                  # resources
        0s                                               # no error
        -1s                                              # error message
        [0x04]                                           # broker resource
        1s "0"                                           # "0" nodeId
        2                                                # configs
          28s "group.min.session.timeout.ms"             # name
          4s "6000"                                      # value
          [0x00]                                          # not read only
          [0x00]                                          # not default
          [0x00]                                          # not sensitive
          28s "group.max.session.timeout.ms"             # name
          5s "30000"                                     # value
          [0x00]                                          # not read only
          [0x00]                                          # not default
          [0x00]                                          # not sensitive

read notify ROUTED_DESCRIBE_SERVER

connect await ROUTED_DESCRIBE_SERVER
        "zilla://streams/net0"
  option zilla:window ${networkConnectWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

connected

write 119                                # size
      11s                               # join group
      5s                                # v5
      ${newRequestId}
      5s "zilla"                        # client id
      4s "test"                         # consumer group
      30000                             # session timeout
      4000                              # rebalance timeout
      0s                                # consumer group member
      42s ${instanceId}                 # group instance id
      8s "consumer"                     # protocol type
      1                                 # group protocol
        10s "highlander"                    # protocol name
        14                                   # metadata size
        ${kafka:randomBytes(14)}             # metadata

read  34                                                # size
      (int:newRequestId)
      0                                                 # throttle time
      79s                                               # member id required
      -1                                                # generated id
      0s                                                # protocol name
      0s                                                # leader id
      10s "memberId-1"                                  # consumer member group id
      0                                                 # members

write 129                               # size
      11s                               # join group
      5s                                # v5
      ${newRequestId}
      5s "zilla"                        # no client id
      4s "test"                         # consumer group
      30000                             # session timeout
      4000                              # rebalance timeout
      10s "memberId-1"                  # consumer group member
      42s ${instanceId}                 # group instance id
      8s "consumer"                     # protocol type
      1                                 # group protocol
        10s "highlander"                    # protocol name
        14                                   # metadata size
        ${kafka:randomBytes(14)}             # metadata

read  128                                               # size
      (int:newRequestId)
      0                                                 # throttle time
      0s                                                # no error
      3                                                 # generated id
      10s "highlander"                                  # protocol name
      10s "memberId-1"                                  # leader id
      10s "memberId-1"                                  # consumer member group id
      1                                                 # members
         10s "memberId-1"                                  # consumer member group id
         42s [0..42]                                       # group instance id
         14                                                # metadata size
         2s                                                # version
         0                                                 # topics
         0                                                 # userdata
         0                                                 # partitions

write 101                                            # size
      14s                                           # sync group
      3s                                            # v3
      ${newRequestId}
      5s "zilla"                                    # no client id
      4s "test"                                     # consumer group
      3                                             # generation id
      10s "memberId-1"                              # consumer member group id
      42s ${instanceId}                             # group instance id
      1                                             # assignments
        10s "memberId-1"                             # consumer member group id
        0                                            # metadata

read  14                                                # size
      (int:newRequestId)
      0                                                 # throttle time
      0s                                                # no error
      0                                                 # assignment

write 81                                            # size
      12s                                           # heartbeat
      3s                                            # v3
      ${newRequestId}
      5s "zilla"                                    # no client id
      4s "test"                                     # consumer group
      3                                             # generation id
      10s "memberId-1"                              # consumer member group id
      42s ${instanceId}                             # group instance id

read  10                                                # size
      (int:newRequestId)
      0                                                 # throttle time
      25s                                               # unknown member id

write 119                               # size
      11s                               # join group
      5s                                # v5
      ${newRequestId}
      5s "zilla"                        # client id
      4s "test"                         # consumer group
      30000                             # session timeout
      4000                              # rebalance timeout
      0s                                # consumer member group id
      42s ${instanceId}                 # group instance id
      8s "consumer"                     # protocol type
      1                                 # group protocol
        10s "highlander"                    # protocol name
        14                                  # metadata size
        ${kafka:randomBytes(14)}            # metadata


read 128                                               # size
      (int:newRequestId)
      0                                                 # throttle time
      0s                                                # no error
      3                                                 # generated id
      10s "highlander"                                  # protocol name
      10s "memberId-2"                                  # leader id
      10s "memberId-2"                                  # consumer member group id
      1                                                 # members
         10s "memberId-2"                                  # consumer member group id
         42s [0..42]                                       # group instance id
         14                                                # metadata size
         2s                                                # version
         0                                                 # topics
         0                                                 # userdata
         0                                                 # partitions
write 101                                           # size
      14s                                           # sync group
      3s                                            # v3
      ${newRequestId}
      5s "zilla"                                    # client id
      4s "test"                                     # consumer group
      3                                             # generation id
      10s "memberId-2"                              # consumer member group id
      42s ${instanceId}                             # group instance id
      1                                             # assignments
        10s "memberId-2"                             # consumer member group id
        0                                            # metadata

read  14                                                # size
      (int:newRequestId)
      0                                                 # throttle time
      0s                                                # no error
      0                                                 # assignment

write 81                                            # size
      13s                                           # leave group
      3s                                            # v3
      ${newRequestId}
      5s "zilla"                                    # client id
      4s "test"                                     # consumer group
      1                                             # assignments
        10s "memberId-2"                             # consumer member group id
        42s ${instanceId}                            # group instance id

read  72                                           # size
      (int:newRequestId)
      0                                            # throttle time
      0s                                           # no error
      1                                            # assignments
        10s "memberId-2"                             # consumer member group id
        42s [0..42]                                  # group instance id
        0s                                           # no error

