#
# Copyright 2021-2023 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property networkConnectWindow 8192

property newRequestId ${kafka:newRequestId()}
property fetchWaitMax 500
property fetchBytesMax 65535
property partitionBytesMax 8192

connect "zilla://streams/net0"
  option zilla:window ${networkConnectWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

connected

write 28                                # size
      17s                               # sasl.handshake
      1s                                # v1
      ${newRequestId}
      5s "zilla"                        # client id
      11s "SCRAM-SHA-1"               # mechanism

read 23                                 # size
     ${newRequestId}
     0s                                 # no error
     1                                  # mechanisms
       11s "SCRAM-SHA-1"              # SCRAM

write 55                                # size
      36s                               # sasl.authenticate
      1s                                # v1
      ${newRequestId}
      5s "zilla"                        # client id
      36                                # authentication bytes
      "n,,n=user,r=fyko+d2lbbFgONRv9qkxdawL"

read 92                                 # size
     ${newRequestId}
     0s                                 # no error
     -1s
     70 "r=fyko+d2lbbFgONRv9qkxdawL3rfcNHYJY1ZVvWVs7j,s=QSXCR+Q6sek8bf92,i=4096"
     0L                                 # session lifetime

write 101                               # size
      36s                               # sasl.authenticate
      1s                                # v1
      ${newRequestId}
      5s "zilla"                        # client id
      82                                # authentication bytes
      "c=biws,r=fyko+d2lbbFgONRv9qkxdawL3rfcNHYJY1ZVvWVs7j,p=v0X8v3Bz2T0CJGbJQyF0X+HI4Ts="

read 52                                 # size
     ${newRequestId}
     0s                                 # no error
     -1s
     30 "v=rmF9pqV8S7suAoZWja4dJRkFsKQ="
     0L                                 # session lifetime

write 18                                # size
      60s                               # describe cluster
      0s                                # v0
      ${newRequestId}
      5s "zilla"                        # client id
      [0x00]                            # tagged fields
      [0x00]                            # include cluster authorized ops
      [0x00]                            # tagged fields

read  92                                 # size
      (int:newRequestId)
      [0x00]                             # tagged fields
      0                                  # throttle time ms
      0s                                 # error code
      [0x00]                             # error message
      [0x0a] "cluster-0"                 # cluster id
      0                                  # controller id
      [0x03]                             # brokers
        1                                # broker id
        [0x14] "broker1.example.com"     # host
        9092                             # port
        [0x00]                           # rack
        [0x00]                           # tagged fields
        2                                # broker id
        [0x14] "broker2.example.com"     # host
        9092                             # port
        [0x00]                           # rack
        [0x00]                           # tagged fields
      0                                  # cluster authorized operations
      [0x00]                             # tagged fields
