#
# Copyright 2021-2023 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property networkAcceptWindow 8192

property deltaMillis 0L
property newTimestamp ${kafka:timestamp() + deltaMillis}

accept "zilla://streams/net0"
  option zilla:window ${networkAcceptWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

accepted

connected

read 26                                 # size
     3s                                 # metadata
     5s                                 # v5
     (int:requestId)
     5s "zilla"                         # client id
     1                                  # topics
       4s "test"                        #   "test" topic
     [0x00]                             # allow_auto_topic_creation

write 97                                # size
      ${requestId}
      0
      1                                 # brokers
        0xb1                                # broker id
        19s "broker1.example.com"           # host name
        9092                                # port
        -1s                                 # no rack
      9s "cluster 1"                    # cluster id
      1                                 # controller id
      1                                 # topics
        0s                                # no error
        4s "test"                         #   "test" topic
        [0x00]                            # not internal
        1                                 # partitions
          0s                                # no error
          0                                 # partition
          0xb1                              # leader
          0                                 # no replicas
          -1                                # no in-sync replicas
          0                                 # offline replicas

accepted

read zilla:begin.ext ${proxy:matchBeginEx()
                              .typeId(zilla:id("proxy"))
                              .addressInet()
                                .protocol("stream")
                                .source("0.0.0.0")
                                .destination("broker1.example.com")
                                .sourcePort(0)
                                .destinationPort(9092)
                                .build()
                              .info()
                                .authority("broker1.example.com")
                                .build()
                              .build()}

connected

read 70
     1s
     5s
     (int:requestId)
     5s "zilla"                        # client id
     -1
     [0..4]
     1
     [0..4]
     [0x00]
     1
     4s "test"
     1
     0
     10L
     -1L
     [0..4]

write 140
      ${requestId}
      0
      1
      4s "test"
      1                                 # partitions
      0                                 # partition 0
      0s                                # no error
      11L                               # high water mark
      11L                               # last stable offset
      -1L                               # no log start offset
      -1                                # no aborted transaction count
      80                                # record set size
      10L                               # first offset
      68                                # length
      0x00
      [0x02]
      0x4e8723aa
      0s
      0                                 # last offset delta
      ${newTimestamp}                   # first timestamp
      ${newTimestamp}                   # last timestamp
      -1L
      -1s
      -1
      1                                 # records
      ${kafka:varint(18)}
      [0x00]
      ${kafka:varint(0)}
      ${kafka:varint(0)}
      ${kafka:varint(-1)}
      ${kafka:varint(12)}
      "Hello, world"
      ${kafka:varint(0)}
