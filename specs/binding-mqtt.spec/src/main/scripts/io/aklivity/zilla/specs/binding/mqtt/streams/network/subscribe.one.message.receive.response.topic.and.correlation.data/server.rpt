#
# Copyright 2021-2023 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

accept "zilla://streams/net0"
  option zilla:window 8192
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

accepted
connected

read  [0x10 0x18]                   # CONNECT
      [0x00 0x04] "MQTT"            # protocol name
      [0x05]                        # protocol version
      [0x02]                        # flags = clean start
      [0x00 0x3c]                   # keep alive = 60s
      [0x05]                        # properties
      [0x27] 66560                  # maximum packet size = 66560
      [0x00 0x06] "client"          # client id

write [0x20 0x08]                   # CONNACK
      [0x00]                        # flags = none
      [0x00]                        # reason code
      [0x05]                        # properties = none
      [0x27] 66560                  # maximum packet size = 66560

read  [0x82 0x12]                   # SUBSCRIBE
      [0x00 0x01]                   # packet id = 1
      [0x02]                        # properties
      [0x0b 0x01]                   # subscription id = 1
      [0x00 0x0a] "sensor/one"      # topic filter
      [0x20]                        # options = at-most-once

write [0x90 0x04]                   # SUBACK
      [0x00 0x01]                   # packet id = 1
      [0x00]                        # properties = none
      [0x00]                        # reason code

write [0x30 0x3b]                   # PUBLISH
      [0x00 0x0a] "sensor/one"      # topic name
      [0x27]                        # properties
      [0x0b 0x01]                   # subscription id = 1
      [0x02] 0x0f                   # expiry = 15 seconds
      [0x03 0x00 0x07] "message"    # content type
      [0x01 0x01]                   # format = utf-8
      [0x08 0x00 0x0a] "sensor/one" # response topic
      [0x09 0x00 0x04] "info"       # correlation data
      "message"                     # payload
