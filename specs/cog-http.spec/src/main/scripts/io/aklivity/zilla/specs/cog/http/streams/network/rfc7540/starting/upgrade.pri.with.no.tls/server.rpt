#
# Copyright 2021-2021 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

accept "zilla://streams/net#0"
       option zilla:window 8192
       option zilla:transmission "duplex"
accepted

read zilla:begin.ext ${proxy:matchBeginEx()
                            .typeId(zilla:id("proxy"))
                            .build()}

connected

# server connection preface - SETTINGS frame
write [0x00 0x00 0x12]                   # length = 18
      [0x04]                             # HTTP2 SETTINGS frame
      [0x00]                             # flags = 0x00
      [0x00 0x00 0x00 0x00]              # stream_id = 0
      [0x00 0x03 0x00 0x00 0x00 0x64]    # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100
      [0x00 0x04 0x00 0x00 0x00 0x00]    # SETTINGS_INITIAL_WINDOW_SIZE(0x03) = 0
      [0x00 0x06 0x00 0x00 0x20 0x00]    # SETTINGS_MAX_HEADER_LIST_SIZE(0x06) = 8192
write flush

# client connection preface
read "PRI * HTTP/2.0\r\n"
     "\r\n"
     "SM\r\n"
     "\r\n"

read  [0x00 0x00 0x0c]                   # length = 12
      [0x04]                             # HTTP2 SETTINGS frame
      [0x00]                             # flags = 0x00
      [0x00 0x00 0x00 0x00]              # stream_id = 0
      [0x00 0x03 0x00 0x00 0x00 0x64]    # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100
      [0x00 0x04 0x00 0x00 0xff 0xff]    # SETTINGS_INITIAL_WINDOW_SIZE(0x04) = 65535

write [0x00 0x00 0x00]                  # length = 0
      [0x04]                            # HTTP2 SETTINGS frame
      [0x01]                            # ACK
      [0x00 0x00 0x00 0x00]             # stream_id = 0
write flush

read [0x00 0x00 0x33]                                       # length = 51
     [0x01]                                                 # HEADERS frame
     [0x04]                                                 # END_HEADERS
     [0x00 0x00 0x00 0x01]                                  # stream_id = 1
     [0x86]                                                 # :scheme: http
     [0x83]                                                 # :method: POST
     [0x84]                                                 # :path: /
     [0x01] [0x0e] "localhost:8080"                         # :authority: localhost:8080
     [0x0f 0x10] [0x18] "text/plain;charset=UTF-8"          # content-type
     [0x0f 0x0d] [0x02] "12"                                # content-length

read [0x00 0x00 0x0c]                   # length = 12
     [0x00]                             # HTTP2 DATA frame
     [0x01]                             # END_STREAM
     [0x00 0x00 0x00 0x01]              # stream_id = 1
     "Hello, world"

read [0x00 0x00 0x00]                   # length = 0
     [0x04]                             # HTTP2 SETTINGS frame
     [0x01]                             # ACK
     [0x00 0x00 0x00 0x00]              # stream_id = 0

write [0x00 0x00 0x04]                                      # length
      [0x08]                                                # WINDOW_UPDATE frame
      [0x00]                                                # no flags
      [0x00 0x00 0x00 0x00]                                 # stream_id=0
      [0x00 0x00 0x00 0x0c]                                 # window size increment = 12

write [0x00 0x00 0x04]                                      # length
      [0x08]                                                # WINDOW_UPDATE frame
      [0x00]                                                # no flags
      [0x00 0x00 0x00 0x01]                                 # stream_id=1
      [0x00 0x00 0x00 0x0c]                                 # window size increment = 12

write [0x00 0x00 0x58]                                      # length
      [0x01]                                                # HTTP2 HEADERS frame
      [0x04]                                                # END_HEADERS
      [0x00 0x00 0x00 0x01]                                 # stream_id=1
      [0x88]                                                # :status: 200
      [0x0f 0x27] [0x14] "CERN/3.0 libwww/2.17"             # server
      [0x0f 0x12] [0x1d] "Wed, 01 Feb 2017 19:12:46 GMT"    # date
      [0x0f 0x10] [0x18] "text/plain;charset=UTF-8"         # content-type
      [0x0f 0x0d] [0x02] "17"                               # content-length
write flush

write [0x00 0x00 0x11]                  # length = 17
      [0x00]                            # HTTP2 DATA frame
      [0x00]                            # no flags
      [0x00 0x00 0x00 0x01]             # stream_id=1
      "Hello from Zilla!"
write flush
