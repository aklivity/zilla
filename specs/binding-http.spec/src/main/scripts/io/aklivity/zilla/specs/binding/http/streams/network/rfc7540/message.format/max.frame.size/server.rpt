#
# Copyright 2021-2022 Aklivity Inc.
#
# Aklivity licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property data20k ${http:randomBytes(20000)}

accept "zilla://streams/net0"
       option zilla:window 60000
       option zilla:transmission "duplex"
accepted
connected

# client connection preface
read "PRI * HTTP/2.0\r\n"
      "\r\n"
      "SM\r\n"
      "\r\n"

# server connection preface - SETTINGS frame
write [0x00 0x00 0x12]                   # length = 18
      [0x04]                             # HTTP2 SETTINGS frame
      [0x00]                             # flags = 0x00
      [0x00 0x00 0x00 0x00]              # stream_id = 0
      [0x00 0x03 0x00 0x00 0x00 0x64]    # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100
      [0x00 0x04 0x00 0x00 0x00 0x00]    # SETTINGS_INITIAL_WINDOW_SIZE(0x03) = 0
      [0x00 0x06 0x00 0x00 0x20 0x00]    # SETTINGS_MAX_HEADER_LIST_SIZE(0x06) = 8192
write flush

read [0x00 0x00 0x12]                   # length = 18
     [0x04]                             # HTTP2 SETTINGS frame
     [0x00]                             # flags = 0x00
     [0x00 0x00 0x00 0x00]              # stream_id = 0
     [0x00 0x03 0x00 0x00 0x00 0x64]    # SETTINGS_MAX_CONCURRENT_STREAMS(0x03) = 100
     [0x00 0x04 0x00 0x00 0x71 0x48]    # SETTINGS_INITIAL_WINDOW_SIZE(0x04) = 29000
     [0x00 0x05 0x00 0x00 0x4e 0x20]    # SETTINGS_MAX_FRAME_SIZE(0x05) = 20000

write [0x00 0x00 0x00]                   # length = 0
      [0x04]                             # HTTP2 SETTINGS frame
      [0x01]                             # ACK
      [0x00 0x00 0x00 0x00]              # stream_id = 0
write flush

#
# request headers with HEADERS frames
#
read [0x00 0x00 0x13]                  # length = 19
     [0x01]                            # HEADERS frame
     [0x05]                            # END_HEADERS | END_STREAM
     [0x00 0x00 0x00 0x01]             # stream_id = 1
     [0x82]                            # :method: GET
     [0x86]                            # :scheme: http
     [0x84]                            # :path: /
     [0x01] [0x0e] "localhost:8080"    # :authority: localhost:8080

read [0x00 0x00 0x00]                  # length = 0
     [0x04]                            # HTTP2 SETTINGS frame
     [0x01]                            # ACK
     [0x00 0x00 0x00 0x00]             # stream_id = 0

#
# response HEADERS frame
#
write [0x00 0x00 0x5b]                                      # length
      [0x01]                                                # HTTP2 HEADERS frame
      [0x04]                                                # END_HEADERS
      [0x00 0x00 0x00 0x01]                                 # stream_id=1
      [0x88]                                                # :status: 200
      [0x0f 0x27] [0x14] "CERN/3.0 libwww/2.17"             # server
      [0x0f 0x12] [0x1d] "Wed, 01 Feb 2017 19:12:46 GMT"    # date
      [0x0f 0x10] [0x18] "text/html; charset=UTF-8"         # content-type
      [0x0f 0x0d] [0x05] "30000"                            # content-length
write flush

#
# response DATA frame
#
write [0x00 0x4e 0x20]                          # length = 20000
      [0x00]                                    # HTTP2 DATA frame
      [0x00]                                    # no flags
      [0x00 0x00 0x00 0x01]                     # stream_id=1
write ${data20k}
write flush

#
# response DATA frame 9000 = max(30000 - 20000, 29000 - 20000)
#
write [0x00 0x1f 0x2e]                          # length = 7982
      [0x00]                                    # HTTP2 DATA frame
      [0x00]                                    # no flags
      [0x00 0x00 0x00 0x01]                     # stream_id=1
write ${http:randomBytes(7982)}
write flush

# connection-level flow control
read [0x00 0x00 0x04]                           # length
     [0x08]                                     # WINDOW_UPDATE frame
     [0x00]                                     # no flags
     [0x00 0x00 0x00 0x00]                      # stream_id=0
     [0x00 0x00 0x79 0x18]                      # window size increment = 31000

# stream-level flow control
read [0x00 0x00 0x04]                           # length
     [0x08]                                     # WINDOW_UPDATE frame
     [0x00]                                     # no flags
     [0x00 0x00 0x00 0x01]                      # stream_id=1
     [0x00 0x00 0x79 0x18]                      # window size increment = 31000

write [0x00 0x00 0x09]                          # length = 9
      [0x00]                                    # HTTP2 DATA frame
      [0x00]                                    # no flags
      [0x00 0x00 0x00 0x01]                     # stream_id=1
write ${http:randomBytes(9)}
write flush

write [0x00 0x07 0xd9]                          # length = 2009
      [0x00]                                    # HTTP2 DATA frame
      [0x00]                                    # no flags
      [0x00 0x00 0x00 0x01]                     # stream_id=1
write ${http:randomBytes(2009)}
write flush
