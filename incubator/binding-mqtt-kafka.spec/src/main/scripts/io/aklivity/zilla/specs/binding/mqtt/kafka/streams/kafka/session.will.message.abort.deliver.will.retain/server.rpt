#
# Copyright 2021-2023 Aklivity Inc
#
# Licensed under the Aklivity Community License (the "License"); you may not use
# this file except in compliance with the License.  You may obtain a copy of the
# License at
#
#   https://www.aklivity.io/aklivity-community-license/
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OF ANY KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations under the License.
#

accept "zilla://streams/kafka0"
        option zilla:window 8192
        option zilla:transmission "duplex"


accepted

read zilla:begin.ext ${kafka:matchBeginEx()
                            .typeId(zilla:id("kafka"))
                            .merged()
                                .capabilities("PRODUCE_AND_FETCH")
                                .topic("mqtt_sessions")
                                .groupId("mqtt-clients")
                                .filter()
                                    .header("type", "will-signal")
                                    .build()
                                .build()
                            .build()}

connected

write await RECEIVED_WILL_DELIVER_LATER_SIGNAL
write zilla:data.ext ${kafka:dataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will-signal")
                               .header("type", "will-signal")
                               .build()
                           .build()}
write ${mqtt:willSignal()
            .clientId("client-1")
            .delay(1)
            .deliverAt(-1)
            .lifetimeId("1")
            .willId("1")
        .build()}
write flush

# will signal for client-1, deliver at (now + delay)
write await RECEIVED_WILL_DELIVER_AT_SIGNAL
write zilla:data.ext ${kafka:dataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will-signal")
                               .header("type", "will-signal")
                               .build()
                           .build()}
write ${mqtt:willSignal()
            .clientId("client-1")
            .delay(1)
            .deliverAt(2)
            .lifetimeId("1")
            .willId("1")
        .build()}
write flush

#Once the will is delivered -> remove will message + signal
read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will-signal")
                               .hashKey("client-1")
                               .header("type", "will-signal")
                               .build()
                           .build()}
read zilla:data.null

read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will")
                               .hashKey("client-1")
                               .build()
                           .build()}
read zilla:data.null

# non-clean start -> fetch lifetimeId
accepted

read zilla:begin.ext ${kafka:matchBeginEx()
                            .typeId(zilla:id("kafka"))
                            .merged()
                                .capabilities("FETCH_ONLY")
                                .topic("mqtt_sessions")
                                .filter()
                                    .key("client-1#will-signal")
                                    .build()
                                .build()
                            .build()}

connected

# no will signals
write advise zilla:flush

read closed
write close


# send initial migrate signal
accepted

read zilla:begin.ext ${kafka:matchBeginEx()
                            .typeId(zilla:id("kafka"))
                            .merged()
                                .capabilities("PRODUCE_AND_FETCH")
                                .topic("mqtt_sessions")
                                .filter()
                                    .key("client-1#migrate")
                                    .headerNot("sender-id", "sender-1")
                                    .build()
                                .build()
                            .build()}

connected

read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#migrate")
                               .hashKey("client-1")
                               .header("sender-id", "sender-1")
                               .build()
                           .build()}
read zilla:data.empty

read closed
write close


accepted

read zilla:begin.ext ${kafka:matchBeginEx()
                               .typeId(zilla:id("kafka"))
                               .group()
                                   .groupId("client-1")
                                   .protocol("highlander")
                                   .timeout(1000)
                                   .build()
                               .build()}

connected

write zilla:data.ext ${kafka:dataEx()
                             .typeId(zilla:id("kafka"))
                             .group()
                                 .leaderId("member-1")
                                 .memberId("member-1")
                                 .members(1)
                                 .build()
                             .build()}
write flush

read aborted


accepted

read zilla:begin.ext ${kafka:matchBeginEx()
                            .typeId(zilla:id("kafka"))
                            .merged()
                                .capabilities("PRODUCE_AND_FETCH")
                                .topic("mqtt_sessions")
                                .filter()
                                    .key("client-1")
                                    .build()
                                .filter()
                                    .key("client-1#migrate")
                                    .headerNot("sender-id", "sender-1")
                                    .build()
                                .build()
                            .build()}

connected

# no session state
# no migrate signals

write advise zilla:flush

# will message for client-1
read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will")
                               .hashKey("client-1")
                               .build()
                           .build()}
read ${mqtt:will()
           .topic("obituaries")
           .delay(1)
           .flags("RETAIN")
           .format("TEXT")
           .payload("client-1 disconnected abruptly")
           .lifetimeId("1")
           .willId("1")
      .build()}

# will signal for client-1, deliver later
read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will-signal")
                               .hashKey("client-1")
                               .header("type", "will-signal")
                               .build()
                           .build()}
read ${mqtt:willSignal()
           .clientId("client-1")
           .delay(1)
           .deliverAt(-1)
           .lifetimeId("1")
           .willId("1")
       .build()}

write notify RECEIVED_WILL_DELIVER_LATER_SIGNAL

# will signal for client-1, deliver at (now + delay)
read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will-signal")
                               .hashKey("client-1")
                               .build()
                           .build()}
#TODO: deliverAt: now() + will delay, where we provide a timeSupplier from the test
read ${mqtt:willSignal()
           .clientId("client-1")
           .delay(1)
           .deliverAt(2)
           .lifetimeId("1")
           .willId("1")
       .build()}

write notify RECEIVED_WILL_DELIVER_AT_SIGNAL

read aborted
write abort


accepted

read zilla:begin.ext ${kafka:matchBeginEx()
                            .typeId(zilla:id("kafka"))
                            .merged()
                                .capabilities("FETCH_ONLY")
                                .topic("mqtt_sessions")
                                .filter()
                                    .key("client-1#will")
                                    .build()
                                .build()
                            .build()}

connected

# saved will message for client-1
write zilla:data.ext ${kafka:dataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("client-1#will")
                               .build()
                           .build()}
write ${mqtt:will()
            .topic("obituaries")
            .delay(1)
            .flags("RETAIN")
            .format("TEXT")
            .payload("client-1 disconnected abruptly")
            .lifetimeId("1")
            .willId("1")
        .build()}
write flush

read closed
write close


accepted

read zilla:begin.ext ${kafka:beginEx()
                            .typeId(zilla:id("kafka"))
                            .merged()
                                .capabilities("PRODUCE_ONLY")
                                .topic("mqtt_messages")
                                .partition(-1, -2)
                                .ackMode("LEADER_ONLY")
                                .build()
                            .build()}


connected

# deliver will message for client-1
read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("obituaries")
                               .header("zilla:topic", "wills")
                               .header("zilla:topic", "one")
                               .header("zilla:format", "TEXT")
                               .build()
                           .build()}
read "client-1 disconnected abruptly"

read closed
write close

accepted

read zilla:begin.ext ${kafka:beginEx()
                            .typeId(zilla:id("kafka"))
                            .merged()
                                .capabilities("PRODUCE_ONLY")
                                .topic("mqtt_retained")
                                .partition(-1, -2)
                                .ackMode("LEADER_ONLY")
                                .build()
                            .build()}


connected

# deliver will message for client-1 (retained)
read zilla:data.ext ${kafka:matchDataEx()
                           .typeId(zilla:id("kafka"))
                           .merged()
                               .deferred(0)
                               .partition(-1, -1)
                               .key("obituaries")
                               .header("zilla:topic", "wills")
                               .header("zilla:topic", "one")
                               .header("zilla:format", "TEXT")
                               .build()
                           .build()}
read "client-1 disconnected abruptly"

read closed
write close
