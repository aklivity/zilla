#
# Copyright 2021-2024 Aklivity Inc
#
# Licensed under the Aklivity Community License (the "License"); you may not use
# this file except in compliance with the License.  You may obtain a copy of the
# License at
#
#   https://www.aklivity.io/aklivity-community-license/
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OF ANY KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations under the License.
#

property networkConnectWindow 8192

connect "zilla://streams/net0"
  option zilla:window ${networkConnectWindow}
  option zilla:transmission "duplex"
  option zilla:byteorder "network"

connected

write 75                                # length
      3s                                # major version
      0s                                # minor version
      "user" [0x00]                     # name
      "root" [0x00]                     # value
      "database" [0x00]                 # name
      "dev" [0x00]                      # value
      "application_name" [0x00]         # name
      "psql" [0x00]                     # value
      "client_encoding" [0x00]          # name
      "UTF8" [0x00]                     # value
      [0x00]                            # end of parameters

read [0x52]                             # type R
     8                                  # length
     0                                  # authentication type

read [0x4b]                             # type K
     12                                 # length
     0                                  # pid
     0                                  # key

read [0x53]                                 # type S
     25                                     # length
     "client_encoding" [0x00]               # name
     "UTF8" [0x00]                          # value

read [0x53]                                 # type S
     35                                     # length
     "standard_conforming_strings" [0x00]   # name
     [0x6f 0x6e 0x00]                       # value

read [0x53]                                 # type S
     25                                     # length
     "server_version" [0x00]                # name
     "9.1.0" [0x00]                         # value

read [0x53]                                 # type S
     27                                     # length
     "application_name" [0x00]              # name
     "zilla" [0x00]                         # value

read [0x5a]                             # type Z
     5                                  # length
     [0x49]                             # status

write [0x51]                            # type Q
      27                                # length
      "SELECT * FROM unknown;"
      [0x00]

read  [0x45]                             # type E
      41                                 # length
      "ERROR"                            # severity
      [0x00]                             # end of string
      "XX000"                            # code
      [0x00]                             # end of string
      "Failed to run the query."         # code
      [0x00]                             # end of string

read [0x5a]                             # type Z
     5                                  # length
     [0x49]                             # status

read [0x58]                             # type X
     4                                  # length

read closed
write close
